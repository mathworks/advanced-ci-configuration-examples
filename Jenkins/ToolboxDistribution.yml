pipeline {
    agent none
    stages {
        stage('Compile and test MEX on matrix agents') {
            matrix {
                axes {
                    axis {
                        name 'OS'
                        values 'linux', 'windows', 'macos'
                    }
                }
                stages {
                    stage("Compile and test MEX") {
                        // Assumes you have labels corresponding to the OS types
                        agent { label "${OS}" }
                        tools {
                            matlab 'R2024b'
                        }
                        steps {
                            script {
                                runMATLABBuild(tasks: 'mex test')
                                junit testResults: 'test-results/results.xml', skipPublishingChecks: true
                                stash includes: 'toolbox/*.mex*', name: "mex-${OS}"
                            }
                        }
                    }
                }
            }
        }
        stage('Create and release toolbox') {
            agent { label 'linux' }
            environment {
                GITHUB_TOKEN = credentials('github-token') // Store your GitHub token as a Jenkins credential
            }
            tools {
                matlab 'R2024b'
            }
            steps {
                script {
                    // Loop through agents to unstash files
                    def agents = ['linux', 'windows', 'macos']
                    agents.each { OS ->
                        unstash "mex-${OS}"
                    }
                }
                runMATLABBuild(tasks: 'packageToolbox')
                script {
                    // Create GitHub release
                    sh "gh release create v1.${env.BUILD_NUMBER}.0 toolbox.mltbx --title 'Cross-Platform Array Product Toolbox' --repo ${env.GIT_URL}"
                }
            }
        }
    } 
}